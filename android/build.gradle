plugins {
    id "com.android.library" version "$versions_agp"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 29
        versionName project.version
        consumerProguardFiles 'consumer-proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(':java')

    implementation "androidx.annotation:annotation:$versions_androidx"
}

tasks.register("javadoc", Javadoc) {
    source += android.sourceSets.main.java.sourceFiles
    classpath += files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.releaseCompileClasspath
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    inputs.files(javaPreCompileRelease)
    outputs.dir(destinationDir)
}

def sourcesTask = tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.register("javadocJar", Jar) {
    archiveClassifier = 'javadoc'
    from tasks.named('javadoc')
}

configurations {
    sourcesElements {
        attributes {
            attribute(Attribute.of("org.gradle.docstype", String), DocsType.SOURCES)
        }
    }
}

artifacts {
    sourcesElements(sourcesTask) {
        builtBy(sourcesJar)
    }
}

afterEvaluate {
    components.release.addVariantsFromConfiguration(configurations.sourcesElements) {
        mapToMavenScope("compile")
    }
    components.release.withVariantsFromConfiguration(configurations.releaseApiPublication) {
        if (configurationVariant.artifacts.isEmpty()) { skip() }
    }
    components.release.withVariantsFromConfiguration(configurations.releaseRuntimePublication) {
        if (configurationVariant.artifacts.isEmpty()) { skip() }
    }
    publishing {
        publications {
            bintray(MavenPublication) {
                from components.release

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}