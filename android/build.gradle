plugins {
    id "com.android.library" version "$versions_agp"
    id "kotlin-android" version "$versions_kotlin"
    id "org.jetbrains.dokka" version "$versions_dokka"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 17
        versionName project.version
        consumerProguardFiles 'consumer-proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs += "src/main/kotlin"
        }
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions_kotlin"
}

configurations {
    sourcesElements {
        attributes {
            attribute(Attribute.of("org.gradle.docstype", String), DocsType.SOURCES)
        }
    }
}

tasks.named("dokka").configure {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

def javadocTask = tasks.register("dokkaJar", Jar) {
    archiveClassifier = 'javadoc'
    from tasks.named('dokka')
}

def sourcesTask = tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    sourcesElements(sourcesTask) {
        builtBy(sourcesJar)
    }
}

tasks.named("bintrayUpload").configure {
    dependsOn javadocTask
}

afterEvaluate {
    components.release.addVariantsFromConfiguration(configurations.sourcesElements) {
        mapToMavenScope("compile")
    }
    components.release.withVariantsFromConfiguration(configurations.releaseApiPublication) {
        if (configurationVariant.artifacts.isEmpty()) { skip() }
    }
    components.release.withVariantsFromConfiguration(configurations.releaseRuntimePublication) {
        if (configurationVariant.artifacts.isEmpty()) { skip() }
    }
    publishing {
        publications {
            bintray(MavenPublication) {
                from components.release

                artifact dokkaJar
            }
        }
    }
}